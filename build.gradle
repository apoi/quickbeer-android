apply plugin: 'com.github.ben-manes.versions'

buildscript {
    ext.kotlin_version = '1.3.41'
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.26.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
        classpath 'com.google.gms:google-services:4.3.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.pinterest:ktlint:0.35.0"
}

task ktlintConfigure(type: JavaExec, group: "idea") {
    description = "Configure your IntelliJ IDEA to match Ktlint style"
    classpath = rootProject.configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args = [
            "--android",
            "applyToIDEA",
            "-y"
    ]
}
