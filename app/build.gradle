apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "quickbeer.android"
        versionCode 1
        versionName "1.0"

        minSdkVersion 19
        targetSdkVersion 25
        renderscriptTargetApi 20
        renderscriptSupportModeEnabled true
    }
    dexOptions {
        preDexLibraries false
    }
    signingConfigs {
        release {
            try {
                storeFile rootProject.file("quickbeer.jks")
                storePassword KEYSTORE_PASSWORD
                keyAlias "quickbeer"
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }
    buildTypes {
        debug {
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

retrolambda {
    jvmArgs '-noverify'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Use local in-development version of reark
    debugCompile project(path: ':reark:reark', configuration: 'debug')
    releaseCompile project(path: ':reark:reark', configuration: 'release')

    // Material design search
    compile project(':materialsearch')

    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:cardview-v7:25.1.0'
    compile 'javax.annotation:jsr250-api:1.0'

    // Multiline Collapsing Toolbar
    compile 'net.opacapp:multiline-collapsingtoolbar:1.3.1'

    // Dagger
    apt 'com.google.dagger:dagger-compiler:2.9'
    compile 'com.google.dagger:dagger:2.9'

    // RxJava
    compile 'io.reactivex:rxjava:1.2.6'
    compile 'io.reactivex:rxandroid:1.2.1'

    // IxJava
    compile 'com.github.akarnokd:ixjava:1.0.0-RC5'

    // Optionals
    compile 'com.github.tomaszpolanski:options:1.3.0'

    // Schematic
    apt 'net.simonvt.schematic:schematic-compiler:0.7.0'
    compile 'net.simonvt.schematic:schematic:0.7.0'

    // Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'

    // PhotoView
    compile 'com.github.chrisbanes:PhotoView:1.3.1'

    // Butter Knife
    compile 'com.jakewharton:butterknife:8.5.1'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'

    // Timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    // Joda-Time
    compile 'net.danlew:android.joda:2.9.5.1'

    // Cookie Jar
    compile 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:okhttp-android-support:3.6.0'

    // Auto-Value
    provided "com.google.auto.value:auto-value:1.3"
    apt "com.google.auto.value:auto-value:1.3"

    // Auto-Value GSON
    apt "com.ryanharter.auto.value:auto-value-gson:0.4.6"
    provided "com.ryanharter.auto.value:auto-value-gson:0.4.6"

    // Auto-Value Parcelable
    compile "com.ryanharter.auto.value:auto-value-parcel-adapter:0.2.5"
    apt "com.ryanharter.auto.value:auto-value-parcel:0.2.5"

    // Leak tracing
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    testCompile 'junit:junit:4.12'
}
