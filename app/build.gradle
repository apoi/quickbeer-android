apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

apply from: 'app-config.gradle'

def STRING = "String"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "quickbeer.android"
        versionCode version_code
        versionName "3.2.${version_code}"

        minSdkVersion 17
        targetSdkVersion 28
        multiDexEnabled true
        renderscriptTargetApi 20
        renderscriptSupportModeEnabled true

        buildConfigField(STRING, "RATEBEER_API_KEY", ratebeer_api_key)
    }
    dexOptions {
        preDexLibraries false
    }
    signingConfigs {
        debug {
            storeFile rootProject.file("keys/debug.keystore")
            storePassword "quickbeer"
            keyAlias "quickbeer"
            keyPassword "quickbeer"
        }
        release {
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations {
    ktlint
}

ext {
    versions = [
            kotlin           : kotlin_version,

            // AndroidX
            recyclerview     : '1.0.0',
            appcompat        : '1.0.2',
            material         : '1.0.0',
            cardview         : '1.0.0',
            annotation       : '1.0.2',

            // Google and Firebase
            playServices     : '11.6.2',

            // Rx and chaining
            rxJava           : '2.2.7',
            rxAndroid        : '2.1.1',
            rxExtensions     : '0.20.8',
            ixJava           : '1.0.0',
            options          : '1.3.0',

            // Network
            okHttp           : '3.14.0',
            okHttpAndroid    : '3.13.1',
            retrofit         : '2.5.0',
            cookiejar        : '1.0.1',
            gson             : '2.8.1',

            // UI
            collapsingToolbar: '27.1.1',
            materialSearch   : '1.4.0-1',
            butterknife      : '10.1.0',
            androidSvg       : '1.3',
            photoView        : '2.3.0',
            picasso          : '2.71828',
            glide            : '4.9.0',

            // Frameworks and tools
            reark            : '0.3',
            schematic        : '0.7.0',
            threeTenAbp      : '1.2.0',
            leakCanary       : '1.6.3',
            multidex         : '2.0.1',
            dagger           : '2.21',
            stetho           : '1.5.1',
            timber           : '4.7.1',
            ktlint           : '0.31.0',
    ]
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Ktlint
    ktlint "com.github.shyiko:ktlint:$versions.ktlint"

    implementation "androidx.recyclerview:recyclerview:$versions.recyclerview"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.cardview:cardview:$versions.cardview"
    api "androidx.annotation:annotation:$versions.annotation"

    // Damnit multidex
    implementation "androidx.multidex:multidex:$versions.multidex"

    // Firebase
    implementation "com.google.firebase:firebase-core:$versions.playServices"
    implementation "com.google.firebase:firebase-crash:$versions.playServices"

    // Play services barcode support
    implementation "com.google.android.gms:play-services-vision:$versions.playServices"

    // Reark
    implementation "com.github.reark:reark:$versions.reark"

    // MaterialSearchView
    implementation "com.github.apoi:MaterialSearchView:$versions.materialSearch"

    // Multiline Collapsing Toolbar
    implementation "net.opacapp:multiline-collapsingtoolbar:$versions.collapsingToolbar"

    // Dagger
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid"
    implementation "com.github.akarnokd:rxjava2-extensions:$versions.rxExtensions"

    // IxJava
    implementation "com.github.akarnokd:ixjava:$versions.ixJava"

    // Optionals
    implementation "com.github.tomaszpolanski:options:$versions.options"

    // Schematic
    implementation "net.simonvt.schematic:schematic:$versions.schematic"
    kapt "net.simonvt.schematic:schematic-compiler:$versions.schematic"

    // Picasso
    implementation "com.squareup.picasso:picasso:$versions.picasso"

    // Glide
    implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glide@aar"
    implementation "com.github.bumptech.glide:glide:$versions.glide"
    implementation "com.github.bumptech.glide:annotations:$versions.glide"
    kapt "com.github.bumptech.glide:compiler:$versions.glide"

    // AndroidSVG
    implementation "com.caverock:androidsvg:$versions.androidSvg"

    // PhotoView
    implementation "com.github.chrisbanes:PhotoView:$versions.photoView"

    // Butter Knife
    implementation "com.jakewharton:butterknife:$versions.butterknife"
    kapt "com.jakewharton:butterknife-compiler:$versions.butterknife"

    // Timber
    implementation "com.jakewharton.timber:timber:$versions.timber"

    // ThreeTenABP
    implementation "com.jakewharton.threetenabp:threetenabp:$versions.threeTenAbp"

    // Cookie Jar
    implementation "com.github.franmontiel:PersistentCookieJar:$versions.cookiejar"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$versions.okHttp"
    implementation "com.squareup.okhttp3:okhttp-android-support:$versions.okHttpAndroid"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$versions.retrofit"

    // Stetho
    debugImplementation "com.facebook.stetho:stetho:$versions.stetho"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$versions.stetho"

    // Leak tracing
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakCanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanary"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'
